// Audio
mapsound loop_generic01 120 2
mapsound ghost02 100
mapsound metala 85
altmapsound metalb01 85
altmapsound metalb02 85
altmapsound avalanche 60

// Decal
decalload "forscience"
decalload "navigation/arrow"

// Model
mmodel "../player/berserker"

// Script
newui .TutorialTip [
    uiallowinput 0
    uialign 0 -1
    uicolor 0xEE202020 0.01 0.01 [
        uitext $.tut_tip $uiPad:UM $uiPad:UM
    ]
] []

.displayTutorialTip = [
    uisound "ui/tutorial" 200
    .tut_tip = $arg1
    showui ".TutorialTip"
]

trigger_0 = [
    .tut_tip = "Use the WASD keys to move"
    showui ".TutorialTip"
]

trigger_1 = [
    .displayTutorialTip "Press [^f1SPACE^ff] to jump. Use boxes to leap over that wall and proceed"
]

trigger_2 = [
    .displayTutorialTip "Press [^f1LCTRL^ff] to crouch and advance to the next section"
]

trigger_3 = [
    .displayTutorialTip "Jump forward to overcome obstacles and gaps. Familiarize yourself with the jumping mechanics"
]

trigger_4 = [
    .displayTutorialTip "Collect health items to replenish your health"
]

trigger_5 = [
    .displayTutorialTip "Collect super items like the ^f9Megahealth^ff to overcharge your health"
]

trigger_6 = [
    .displayTutorialTip "Grab power-ups such as ^f6Invulnerability^ff to endure environmental and player damage"
]

trigger_7 = [
    .displayTutorialTip "Collect ammo to unlock new weapons, press [^f1LEFT MOUSE BUTTON^ff] to shoot"
    sleep 3500 [
        .displayTutorialTip "Aim down, shoot and jump forward simultaneously to trick-jump"
    ]
]

trigger_8 = [
    .displayTutorialTip "You found a ^f0rocket launcher^ff: aim at your feet, shoot and jump simultaneously to rocket-jump"
]

trigger_9 = [
    .displayTutorialTip "Most weapons have a secondary fire: press [^f1RIGHT MOUSE BUTTON^ff] to use it"
]

trigger_10 = [
    .displayTutorialTip "Use the ^f1number keys^ff on your keyboard or the ^f1scrollwheel^ff to switch through weapons"
]

trigger_11 = [
    .displayTutorialTip "Press [^f1X^ff] to show the middle finger"
]

trigger_12 = [
    .displayTutorialTip "^f0Well done!^ff One more"
]

trigger_13 = [
    .displayTutorialTip "Press [^f1F^ff] for a quick melee attack"
]
