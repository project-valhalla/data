// Textures
texturereset

setshader stdworld
texsky
texture 0 "test/wall_c.png"

setshader "bumpspecmapworld"
setshaderparam "specscale" .8 .8 .8
texture 0 "test/wall_d.png" 0 0 0 1.25
texture s "test/wall_s.png"  0 0 0 1.25
texture n "test/wall_n.png" 0 0 0 1.25
texture 0 "test/floor_d.png" 0 0 0 1.25
texture s "test/floor_s.png" 0 0 0 1.25
texture n "test/floor_n.png" 0 0 0 1.25

// Models
mapmodelreset

mapmodel "valhalla/reconstructor"
mapmodel "valhalla/railings"
mapmodel "valhalla/box"
mapmodel "valhalla/console"
mapmodel "prop/door"
mapmodel "door/flip"

// Sound.
mapsound "loop_generic03"
mapsound "metalc01" 80
altmapsound "metalc02" 80
altmapsound "metalc03" 80
altmapsound "metalc04" 80
mapsound "../story/door01" 200
mapsound "../story/door03" 200
mapsound "../weapon/detonate"
mapsound "../item/pickup_interest"
mapsound "../story/door04" 200
mapsound "../story/door04_close" 200
mapsound "freesound/loop_alarm" 180
mapsound "freesound/alarm" 180
mapsound "loop_hum02" 200
mapsound "../story/machine_on" 200
mapsound "../story/machine_off" 200

// Scripts.

spkillsend 0

eventhandlerreset

trigger "showInteraction" "proximity" [
    local interactBind
    interactBind = (searchbinds "interact")
    .displayStoryInteraction (+s "Press [^f1" $interactBind "^ff] to use")
]
trigger "showInteraction" "distance" [
    hideui .storyInteraction
]

trigger "guard checkpoint" "proximity" [
    .displayStoryTip "Kill the guard to open the door" 0 2
]

monster "guard" "death" [
    .displayStoryTip "Reach the terminal"
    sleep 500 [
        local door marker
        door = (entquery "mapmodel" "guardDoor")
        triggermapmodel $door 1
        triggersound $door 3
        marker = (entquery "trigger" "terminal_02Marker")
        triggerstate $marker 1
    ]
]

trigger "terminal_01" "use" [
    local marker1 marker2 door
    marker1 = (entquery "trigger" "terminalMarker")
    triggerstate $marker1 0
    marker2 = (entquery "trigger" "coinMarker")
    triggerstate $marker2 1
    .displayStoryTip "Collect the coin to activate the other terminal"
    door = (entquery "mapmodel" "coinDoor")
    triggermapmodel $door 1
    triggersound $door 2
    triggersound $eventsource 4
]

.hasMapTestCoin = 0

trigger "coin" "proximity" [
    .hasMapTestCoin = 1
    triggerpickupeffects
    triggersound $eventsource 5
    local marker1 marker2 triggerBox
    marker1 = (entquery "trigger" "coinMarker")
    triggerstate $marker1 0
    marker2 = (entquery "trigger" "terminal_02Marker")
    triggerstate $marker2 1
    triggerBox = (entquery "trigger" "terminal_02")
    triggerstate $triggerBox 1
    .displayStoryTip "Reach the terminal"
]

trigger "terminal_02" "use" [
    if $.hasMapTestCoin [
        local marker1 marker2 door
        marker1 = (entquery "trigger" "regularDoorMarker")
        triggerstate $marker1 1
        marker2 = (entquery "trigger" "terminal_02Marker")
        triggerstate $marker2 0
        door = (entquery "mapmodel" "terminal_02Door")
        triggermapmodel $door 1
        triggersound $door 2
        .displayStoryTip "Activate the gate to escape the facility" 0 2
    ] [
        local marker marker2
        .displayStoryTip "This terminal needs a coin to work"
        marker = (entquery "trigger" "terminalMarker")
        triggerstate $marker 1
        marker2 = (entquery "trigger" "terminal_02Marker")
        triggerstate $marker2 0
    ]
    triggersound $eventsource 4
]
trigger "terminal_02" "distance" [
    if (! $.hasMapTestCoin) [
        .displayStoryTip "Find another terminal!" 0 2
    ]
]

trigger "regularDoor" "use" [
    local door doorstate marker1 marker2
    door = (entquery "mapmodel" "regularDoor")
    doorstate = (mapmodeltriggerstate $door)
    if (|| [= $doorstate 1] [= $doorstate 2]) [
        triggermapmodel $door 3
        triggersound $door 7
    ] [
        triggermapmodel $door 1
        triggersound $door 6
    ]
    marker1 = (entquery "trigger" "regularDoorMarker")
    triggerstate $marker1 0
    marker2 = (entquery "trigger" "terminal_03Marker")
    triggerstate $marker2 1
]

trigger "reusable" "use" [
    triggerstate $eventsource 1
]

.enableTestMapReconstructor = [
    local light particle mapSound marker marker2 portal
    light = (entquery "light" "reconstructorGlow")
    triggerstate $light 1
    triggersound $light 11
    particle = (entquery "particles" "reconstructorRay")
    triggerstate $particle 1
    mapSound = (entquery "sound" "reconstructorLoop")
    triggerstate $sound 1
    marker = (entquery "trigger" "reconstructorEnd")
    triggerstate $marker 1
    marker2 = (entquery "trigger" "trigger_03Marker")
    triggerstate $marker2 0
    triggerstate $eventsource 0
    portal = (entquery "teleport" "reconstructorPortal")
    triggerstate $portal 1
]

.disableTestMapReconstructor = [
    .displayStoryTip "Security has locked down the gate from the other side!"
    local light
    light = (entquery "light" "reconstructorLight")
    triggerstate $light 0
    triggersound $light 12
    sleep 1200 [
        local door1 door2 respawnPoint mapSound1 mapSound2
        door1 = (entquery "mapmodel" "terminal_03Door")
        triggermapmodel $door1 1
        door2 = (entquery "mapmodel" "terminal_02Door")
        triggermapmodel $door2 0
        respawnPoint = (entquery "trigger" "respawnPoint")
        setrespawnpoint $respawnPoint
        mapSound1 = (entquery "sound" "alarm_01")
        triggerstate $mapSound1 1
        mapSound2 = (entquery "sound" "alarm_02")
        triggerstate $mapSound2 1
        music "hurt_locker" "music"
    ]
]

trigger "terminal_03" "use" [
    local marker
    marker = (entquery "trigger" "terminal_03Marker")
    triggerstate $marker 0
     if (< $.mapBossesKilled $.mapBosses) [
        .disableTestMapReconstructor
     ] [
       .enableTestMapReconstructor
       .displayStoryTip "Escape the facility" 0 2
     ]
     triggersound $eventsource 4
]

monster "guard_02" "death" [
    local door triggerBox
    door = (entquery "mapmodel" "guardDoor_02")
    triggermapmodel $door 1
    triggersound $door 2
    triggerBox = (entquery "trigger" "fightTrigger")
    triggerstate $triggerBox 1
]

trigger "fightTrigger" "proximity" [
    local door1 door2 triggerBox
    door1 = (entquery "mapmodel" "guardDoor_02")
    door2 = (entquery "mapmodel" "terminal_02Door")
    triggermapmodel $door1 0
    triggermapmodel $door2 0
    triggerBox = (entquery "trigger" "terminal_02")
    triggerstate $triggerBox 0
    sleep 1000 [
        local door3
        door3 = (entquery "mapmodel" "guardDoor_03")
        triggermapmodel $door3 1
    ]
    sleep 1500 [
        local door4
        door4 = (entquery "mapmodel" "guardDoor_04")
        triggermapmodel $door4 1
    ]
    setrespawnpoint $eventsource
]

.mapBosses = 4
.mapBossesKilled = 0

monster "finalGuard" "death" [
    .mapBossesKilled = (+ $.mapBossesKilled 1)
    if (>= $.mapBossesKilled $.mapBosses) [
        local door marker light triggerBox
        door = (entquery "mapmodel" "terminal_02Door")
        triggermapmodel $door 1
        triggersound $door 2
        marker = (entquery "trigger" "terminal_03Marker")
        triggerstate $marker 1
        light = (entquery "light" "reconstructorLight")
        triggerstate $light 1
        triggerBox = (entquery "trigger" "terminal_03")
        triggerstate $triggerBox 1
        music "" 2000
    ]
    .displayStoryTip "Activate the gate to escape the facility" 0 2
]

trigger "endOfLevel" "proximity" [
    hideui ".mapTestTip"
    endsp
]

trigger "lightSwitch" "use" [
    local light lightstate box
    light = (entquery "light" "switchableLight")
    lightstate = (triggerstate $light)
    box = (entquery "mapmodel" "triggerBox")
    if (= $lightstate 1) [
        triggerstate $light 0
        triggerstate $box 0
    ] [
        triggerstate $light 1
        triggerstate $box 1
    ]
    triggersound $eventsource 4
]
