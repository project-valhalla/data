// Textures.

    texturereset

    setshader stdworld
    texsky
    texture 0 "wohsiesta/wall.png"

    setshader "bumpspecmapworld"
    setshaderparam "specscale" .8 .8 .8
    texload "wohsiesta/brick01"
    texload "wohsiesta/floor"
    texload "wohsiesta/brick02"

// Models.

    mapmodelreset

    mapmodel "valhalla/reconstructor"
    mapmodel "valhalla/railings"
    mapmodel "valhalla/box"
    mapmodel "valhalla/console"
    mapmodel "prop/door"
    mapmodel "nieb/complex/door/flip"
    mapmodel "valhalla/button"
    mapmodel "prop/lambert"
    mapmodel "prop/sofa"
    mapmodel "prop/table"
    mapmodel "prop/fridge"
    mapmodel "prop/gramophone"
    mapmodel "valhalla/toilet"

// Decals.

    decalload "navigation/arrow"
    decalload "propaganda/iwantyou"
    decalload "propaganda/valhallade"
    decalload "signs/teleport"
    decalload "ambient/stain"

// Sound.

    mapsound "loop_generic03"
    mapsound "metalc01" 80
    altmapsound "metalc02" 80
    altmapsound "metalc03" 80
    altmapsound "metalc04" 80
    mapsound "../story/door01" 200
    mapsound "../story/door03" 200
    mapsound "../weapon/detonate"
    mapsound "../item/pickup_interest"
    mapsound "../story/door04" 200
    mapsound "../story/door04_close" 200
    mapsound "freesound/loop_alarm" 180
    mapsound "freesound/alarm" 180
    mapsound "loop_hum02" 200
    mapsound "../story/machine_on" 200
    mapsound "../story/machine_off" 200
    mapsound "../story/door05" 255
    mapsound "../story/door05_close" 255
    mapsound "../story/toilet" 255
    mapsound "songs/the_prisoners_song" 200

// Scripts.

    // ----------------------------------/
    // Map commands and variables        /         
    // ----------------------------------/

        spkillsend 0    // Do not complete the level if all NPCs died.

// Scripts: triggers.

    eventhandlerreset   // Reset trigger events.

    // ----------------------------------/
    // Triggers: proximity & interaction /
    // ----------------------------------/
    
        trigger "firstTrigger" "proximity" [
            local mark
            mark = (entquery "trigger" "firstMark")
            triggerstate $mark 1
            .displayStoryTip "Enter the vent" 1
        ]

        trigger "ventTrigger" "proximity" [
            local interactKey
            interactKey = (searchbinds "crouch")
            .displayStoryInteraction (+s "Press [^f1" $interactKey "^ff] to crouch")
        ]
        
        trigger "ventTrigger" "distance" [
            hideui .storyInteraction
        ]

        trigger "closeGuardDoor" "proximity" [
            local door
            door = (entquery "mapmodel" "guardDoor")
            triggermapmodel $door 3
            triggersound $door 2
        ]

        trigger "guardCheckpoint" "proximity" [
            .displayStoryTip "Kill the guard to open the door" 0 2
        ]

        trigger "terminal_01" "use" [
            local marker door
            marker = (entquery "trigger" "terminalMarker")
            triggerstate $marker 0
            marker = (entquery "trigger" "coinMarker")  // Update `marker`.
            triggerstate $marker 1
            .displayStoryTip "Collect the coin to activate the other terminal"
            door = (entquery "mapmodel" "coinDoor")
            triggermapmodel $door 1
            triggersound $door 2
            triggersound $eventsource 4
        ]

        .hasMapTestCoin = 0

        trigger "coin" "proximity" [
            .hasMapTestCoin = 1
            triggerpickupeffects
            triggersound $eventsource 5
            local marker triggerEntity
            marker = (entquery "trigger" "coinMarker")
            triggerstate $marker 0
            marker = (entquery "trigger" "terminal_02Marker")   // Update `marker`.
            triggerstate $marker 1
            triggerEntity = (entquery "trigger" "terminal_02")
            triggerstate $triggerEntity 1
            .displayStoryTip "Reach the terminal"
        ]

        trigger "terminal_02" "use" [
            if $.hasMapTestCoin [
                local marker door
                marker = (entquery "trigger" "regularDoorMarker")
                triggerstate $marker 1
                marker = (entquery "trigger" "terminal_02Marker")   // Update `marker`.
                triggerstate $marker 0
                door = (entquery "mapmodel" "terminal_02Door")
                triggermapmodel $door 1
                triggersound $door 2
                .displayStoryTip "Activate the gate to escape the facility" 0 2
            ] [
                local marker
                .displayStoryTip "This terminal needs a coin to work"
                marker = (entquery "trigger" "terminalMarker")
                triggerstate $marker 1
                marker = (entquery "trigger" "terminal_02Marker")   // Update `marker`.
                triggerstate $marker 0
            ]
            triggersound $eventsource 4
        ]

        trigger "terminal_02" "distance" [
            if (! $.hasMapTestCoin) [
                .displayStoryTip "Find another terminal!" 0 2
            ]
        ]

        trigger "regularDoor" "use" [
            local marker door doorstate
            marker = (entquery "trigger" "regularDoorMarker")
            triggerstate $marker 0
            door = (entquery "mapmodel" "regularDoor")
            doorstate = (mapmodeltriggerstate $door)
            if (|| [= $doorstate 1] [= $doorstate 2]) [
                triggermapmodel $door 3
                triggersound $door 7
            ] [
                triggermapmodel $door 1
                marker = (entquery "trigger" "terminal_03Marker")
                triggerstate $marker 1
                triggersound $door 6
            ]
        ]

        .startBossFight = [
            local door respawnPoint mapSound
            door = (entquery "mapmodel" "terminal_03Door")
            triggermapmodel $door 1
            door = (entquery "mapmodel" "terminal_02Door")  // Update `door`.
            triggermapmodel $door 3
            respawnPoint = (entquery "trigger" "respawnPoint")
            setrespawnpoint $respawnPoint
            mapSound = (entquery "sound" "alarm")
            triggerstate $mapSound 1
            music "hurt_locker" "music"
        ]

        .enableTestMapReconstructor = [
            local light particle mapSound marker portal
            light = (entquery "light" "reconstructorGlow")
            triggerstate $light 1
            triggersound $light 11
            light = (entquery "light" "buttonLight")    // Update `light`.
            triggerstate $light 0
            particle = (entquery "particles" "reconstructorRay")
            triggerstate $particle 1
            mapSound = (entquery "sound" "reconstructorLoop")
            triggerstate $sound 1
            marker = (entquery "trigger" "reconstructorEnd")
            triggerstate $marker 1
            triggerstate $eventsource 0
            portal = (entquery "teleport" "reconstructorPortal")
            triggerstate $portal 1
        ]

        .disableTestMapReconstructor = [
            .displayStoryTip "Security has locked down the gate from the other side!"
            local light
            light = (entquery "light" "reconstructorLight")
            triggerstate $light 0
            triggersound $light 12
            light = (entquery "light" "buttonLight")    // Update `light`.
            triggerstate $light 0
            sleep 1200 [
                .startBossFight
            ]
            .triggerAlarm
        ]

        trigger "terminal_03" "use" [
            local marker
            marker = (entquery "trigger" "terminal_03Marker")
            triggerstate $marker 0
            if (< $.mapBossesKilled $.mapBosses) [
                .disableTestMapReconstructor
            ] [
            .enableTestMapReconstructor
            .displayStoryTip "Escape the facility" 0 2
            ]
            triggersound $eventsource 4
        ]

        trigger "bossTrigger" "proximity" [
            local door triggerEntity
            door = (entquery "mapmodel" "guardDoor_02")
            triggermapmodel $door 3
            door = (entquery "mapmodel" "terminal_02Door")  // Update `door`.
            triggermapmodel $door 3
            triggerEntity = (entquery "trigger" "terminal_02")
            triggerstate $triggerEntity 0
            sleep 1000 [
                local door3
                door3 = (entquery "mapmodel" "guardDoor_03")
                triggermapmodel $door3 1
            ]
            sleep 1500 [
                local door4
                door4 = (entquery "mapmodel" "guardDoor_04")
                triggermapmodel $door4 1
            ]
            setrespawnpoint $eventsource
        ]

        .triggerAlarm = [
            local light lightState
            light = (entquery "light" "alarmLight_01")
            lightState = (triggerstate $light)
            if (= $lightState 1) [
                triggerstate $light 0
                light = (entquery "light" "alarmLight_02")  // Update `light`.
                triggerstate $light 0
                light = (entquery "light" "alarmLight_03")  // Update `light`.
                triggerstate $light 0
            ] [
                triggerstate $light 1
                light = (entquery "light" "alarmLight_02")  // Update `light`.
                triggerstate $light 1
                light = (entquery "light" "alarmLight_03")  // Update "light".
                triggerstate $light 1
                triggersound $light 9
            ]
            sleep 2500 [
                .triggerAlarm
            ]
        ]

        trigger "endOfLevel" "proximity" [
            hideui ".mapTestTip"
            endsp
        ]

        trigger "reusable" "use" [
            triggerstate $eventsource 1
        ]

        trigger "showInteraction" "proximity" [
            local interactBind
            interactBind = (searchbinds "interact")
            .displayStoryInteraction (+s "Press [^f1" $interactBind "^ff] to use")
        ]
        trigger "showInteraction" "distance" [
            hideui .storyInteraction
        ]

        trigger "lightSwitch" "use" [
            local light lightState button
            light = (entquery "light" "switchableLight")
            lightState = (triggerstate $light)
            button = (entquery "mapmodel" "button_01")
            if (= $lightState 1) [
                triggerstate $light 0
                triggermapmodel $button 3
            ] [
                triggerstate $light 1
                triggermapmodel $button 1
            ]
            triggersound $eventsource 4
        ]

        trigger "modelSwitch" "use" [
            local box boxState button
            box = (entquery "mapmodel" "triggerBox")
            boxState = (triggerstate $box)
            button = (entquery "mapmodel" "button_02")
            if (= $boxState 1) [
                triggerstate $box 0
                triggermapmodel $button 3
            ] [
                triggerstate $box 1
                triggermapmodel $button 1
            ]
            triggersound $eventsource 4
        ]

        trigger "gramophone" "use" [
            local mapSound
            mapSound = (entquery "sound" "gramophone")
            triggerstate $mapSound 1
        ]

        trigger "autoDoor" "proximity" [
            local door
            door = (entquery "mapmodel" "autoDoor")
            triggermapmodel $door 1
            triggersound $door 13
        ]
        trigger "autoDoor" "distance" [
            local door
            door = (entquery "mapmodel" "autoDoor")
            triggermapmodel $door 3
            triggerstate $eventsource 1
            triggersound $door 14
        ]

        trigger "toilet" "use" [
            triggersound $eventsource 15
        ]

    // ----------------------------------/
    // NPC triggers                      /
    // ----------------------------------/

        monster "guard" "death" [
            .displayStoryTip "Reach the terminal"
            sleep 500 [
                local door marker
                door = (entquery "mapmodel" "guardDoor")
                triggermapmodel $door 1
                triggersound $door 3
                marker = (entquery "trigger" "terminal_02Marker")
                triggerstate $marker 1
            ]
        ]

        monster "guard_02" "death" [
            local door triggerEntity
            door = (entquery "mapmodel" "guardDoor_02")
            triggermapmodel $door 1
            triggersound $door 2
            triggerEntity = (entquery "trigger" "bossTrigger")
            triggerstate $triggerEntity 1
        ]


        .mapBosses = 4
        .mapBossesKilled = 0

        monster "bossGuard" "death" [
            .mapBossesKilled = (+ $.mapBossesKilled 1)
            if (>= $.mapBossesKilled $.mapBosses) [
                local door marker light triggerEntity
                door = (entquery "mapmodel" "terminal_02Door")
                triggermapmodel $door 1
                triggersound $door 2
                marker = (entquery "trigger" "terminal_03Marker")
                triggerstate $marker 1
                light = (entquery "light" "reconstructorLight")
                triggerstate $light 1
                light = (entquery "light" "buttonLight")    // Update `light`.
                triggerstate $light 1
                triggerEntity = (entquery "trigger" "terminal_03")
                triggerstate $triggerEntity 1
                music "" 2000
            ]
            .displayStoryTip "Activate the gate to escape the facility" 0 2
        ]
