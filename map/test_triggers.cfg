// Textures
texturereset

setshader stdworld
texsky
texture 0 "wohsiesta/wall.png"

setshader "bumpspecmapworld"
setshaderparam "specscale" .8 .8 .8
texload "wohsiesta/brick01"
texload "wohsiesta/floor"
texload "wohsiesta/brick02"

// Models
mapmodelreset

mapmodel "valhalla/reconstructor"
mapmodel "valhalla/railings"
mapmodel "valhalla/box"
mapmodel "valhalla/console"
mapmodel "prop/door"
mapmodel "nieb/complex/door/flip"
mapmodel "valhalla/button"
mapmodel prop/lambert
mapmodel prop/sofa
mapmodel prop/table
mapmodel prop/fridge
mapmodel prop/gramophone
mapmodel prop/toilet

// Decals
decalload "navigation/arrow"
decalload "propaganda/iwantyou"
decalload "propaganda/valhallade"
decalload "signs/teleport"
decalload "ambient/stain"

// Sound.
mapsound "loop_generic03"
mapsound "metalc01" 80
altmapsound "metalc02" 80
altmapsound "metalc03" 80
altmapsound "metalc04" 80
mapsound "../story/door01" 200
mapsound "../story/door03" 200
mapsound "../weapon/detonate"
mapsound "../item/pickup_interest"
mapsound "../story/door04" 200
mapsound "../story/door04_close" 200
mapsound "freesound/loop_alarm" 180
mapsound "freesound/alarm" 180
mapsound "loop_hum02" 200
mapsound "../story/machine_on" 200
mapsound "../story/machine_off" 200
mapsound "../story/door05" 255
mapsound "../story/door05_close" 255
mapsound "../story/toilet" 255
mapsound "songs/the_prisoners_song" 200

// Scripts.

spkillsend 0

eventhandlerreset

trigger "firstTrigger" "proximity" [
    local mark
    mark = (entquery "trigger" "firstMark")
    triggerstate $mark 1
    .displayStoryTip "Enter the vent" 1
]

trigger "crouchTrigger" "proximity" [
    local interactBind
    interactBind = (searchbinds "crouch")
    .displayStoryInteraction (+s "Press [^f1" $interactBind "^ff] to crouch")
]
trigger "crouchTrigger" "distance" [
    hideui .storyInteraction
]

trigger "closeGuardDoor" "proximity" [
    local door
    door = (entquery "mapmodel" "guardDoor")
    triggermapmodel $door 3
    triggersound $door 2
]

trigger "showInteraction" "proximity" [
    local interactBind
    interactBind = (searchbinds "interact")
    .displayStoryInteraction (+s "Press [^f1" $interactBind "^ff] to use")
]
trigger "showInteraction" "distance" [
    hideui .storyInteraction
]

trigger "guard checkpoint" "proximity" [
    .displayStoryTip "Kill the guard to open the door" 0 2
]

monster "guard" "death" [
    .displayStoryTip "Reach the terminal"
    sleep 500 [
        local door marker
        door = (entquery "mapmodel" "guardDoor")
        triggermapmodel $door 1
        triggersound $door 3
        marker = (entquery "trigger" "terminal_02Marker")
        triggerstate $marker 1
    ]
]

trigger "terminal_01" "use" [
    local marker door
    marker = (entquery "trigger" "terminalMarker")
    triggerstate $marker 0
    marker = (entquery "trigger" "coinMarker") // Update "marker"
    triggerstate $marker 1
    .displayStoryTip "Collect the coin to activate the other terminal"
    door = (entquery "mapmodel" "coinDoor")
    triggermapmodel $door 1
    triggersound $door 2
    triggersound $eventsource 4
]

.hasMapTestCoin = 0

trigger "coin" "proximity" [
    .hasMapTestCoin = 1
    triggerpickupeffects
    triggersound $eventsource 5
    local marker triggerBox
    marker = (entquery "trigger" "coinMarker")
    triggerstate $marker 0
    marker = (entquery "trigger" "terminal_02Marker") // Update "marker"
    triggerstate $marker 1
    triggerBox = (entquery "trigger" "terminal_02")
    triggerstate $triggerBox 1
    .displayStoryTip "Reach the terminal"
]

trigger "terminal_02" "use" [
    if $.hasMapTestCoin [
        local marker door
        marker = (entquery "trigger" "regularDoorMarker")
        triggerstate $marker 1
        marker = (entquery "trigger" "terminal_02Marker") // Update "marker"
        triggerstate $marker 0
        door = (entquery "mapmodel" "terminal_02Door")
        triggermapmodel $door 1
        triggersound $door 2
        .displayStoryTip "Activate the gate to escape the facility" 0 2
    ] [
        local marker
        .displayStoryTip "This terminal needs a coin to work"
        marker = (entquery "trigger" "terminalMarker")
        triggerstate $marker 1
        marker = (entquery "trigger" "terminal_02Marker") // Update "marker"
        triggerstate $marker 0
    ]
    triggersound $eventsource 4
]
trigger "terminal_02" "distance" [
    if (! $.hasMapTestCoin) [
        .displayStoryTip "Find another terminal!" 0 2
    ]
]

trigger "regularDoor" "use" [
    local marker door doorstate
    marker = (entquery "trigger" "regularDoorMarker")
    triggerstate $marker 0
    door = (entquery "mapmodel" "regularDoor")
    doorstate = (mapmodeltriggerstate $door)
    if (|| [= $doorstate 1] [= $doorstate 2]) [
        triggermapmodel $door 3
        triggersound $door 7
    ] [
        triggermapmodel $door 1
        marker = (entquery "trigger" "terminal_03Marker")
        triggerstate $marker 1
        triggersound $door 6
    ]
]

trigger "reusable" "use" [
    triggerstate $eventsource 1
]

.enableTestMapReconstructor = [
    local light particle mapSound marker portal
    light = (entquery "light" "reconstructorGlow")
    triggerstate $light 1
    triggersound $light 11
    light = (entquery "light" "buttonLight")
    triggerstate $light 1
    particle = (entquery "particles" "reconstructorRay")
    triggerstate $particle 1
    mapSound = (entquery "sound" "reconstructorLoop")
    triggerstate $sound 1
    marker = (entquery "trigger" "reconstructorEnd")
    triggerstate $marker 1
    triggerstate $eventsource 0
    portal = (entquery "teleport" "reconstructorPortal")
    triggerstate $portal 1
    .mapAlarm 0
]

.disableTestMapReconstructor = [
    .displayStoryTip "Security has locked down the gate from the other side!"
    local light
    light = (entquery "light" "reconstructorLight")
    triggerstate $light 0
    triggersound $light 12
    sleep 1200 [
        local door respawnPoint mapSound light
        door = (entquery "mapmodel" "terminal_03Door")
        triggermapmodel $door 1
        door = (entquery "mapmodel" "terminal_02Door") // Update "door"
        triggermapmodel $door 3
        respawnPoint = (entquery "trigger" "respawnPoint")
        setrespawnpoint $respawnPoint
        mapSound = (entquery "sound" "alarm_01")
        triggerstate $mapSound 1
        mapSound = (entquery "sound" "alarm_02")
        triggerstate $mapSound 1
        light = (entquery "light" "buttonLight")
        triggerstate $light 0
        music "hurt_locker" "music"
    ]
    .mapAlarm 1
]

trigger "terminal_03" "use" [
    local marker
    marker = (entquery "trigger" "terminal_03Marker")
    triggerstate $marker 0
     if (< $.mapBossesKilled $.mapBosses) [
        .disableTestMapReconstructor
     ] [
       .enableTestMapReconstructor
       .displayStoryTip "Escape the facility" 0 2
     ]
     triggersound $eventsource 4
]

monster "guard_02" "death" [
    local door triggerBox
    door = (entquery "mapmodel" "guardDoor_02")
    triggermapmodel $door 1
    triggersound $door 2
    triggerBox = (entquery "trigger" "fightTrigger")
    triggerstate $triggerBox 1
]

trigger "fightTrigger" "proximity" [
    local door triggerBox
    door = (entquery "mapmodel" "guardDoor_02")
    triggermapmodel $door 3
    door = (entquery "mapmodel" "terminal_02Door") // Update "door"
    triggermapmodel $door 3
    triggerBox = (entquery "trigger" "terminal_02")
    triggerstate $triggerBox 0
    sleep 1000 [
        local door3
        door3 = (entquery "mapmodel" "guardDoor_03")
        triggermapmodel $door3 1
    ]
    sleep 1500 [
        local door4
        door4 = (entquery "mapmodel" "guardDoor_04")
        triggermapmodel $door4 1
    ]
    setrespawnpoint $eventsource
]

.mapAlarm = [
    local light
    light = (entquery "light" "alarmLight")
    if (= $arg1 1) [
        local lightState
        lightState = (triggerstate $light)
        if (= $lightState 1) [
            triggerstate $light 0
        ] [
            triggerstate $light 1
            triggersound $light 9
        ]
        sleep 2500 [
            .mapAlarm 1
        ]
    ] [
        triggerstate $light 0
    ]
]

.mapBosses = 4
.mapBossesKilled = 0

monster "finalGuard" "death" [
    .mapBossesKilled = (+ $.mapBossesKilled 1)
    if (>= $.mapBossesKilled $.mapBosses) [
        local door marker light triggerBox
        door = (entquery "mapmodel" "terminal_02Door")
        triggermapmodel $door 1
        triggersound $door 2
        marker = (entquery "trigger" "terminal_03Marker")
        triggerstate $marker 1
        light = (entquery "light" "reconstructorLight")
        triggerstate $light 1
        triggerBox = (entquery "trigger" "terminal_03")
        triggerstate $triggerBox 1
        music "" 2000
    ]
    .displayStoryTip "Activate the gate to escape the facility" 0 2
]

trigger "endOfLevel" "proximity" [
    hideui ".mapTestTip"
    endsp
]

trigger "lightSwitch" "use" [
    local light lightState button
    light = (entquery "light" "switchableLight")
    lightState = (triggerstate $light)
    button = (entquery "mapmodel" "button_01")
    if (= $lightState 1) [
        triggerstate $light 0
        triggermapmodel $button 3
    ] [
        triggerstate $light 1
        triggermapmodel $button 1
    ]
    triggersound $eventsource 4
]

trigger "modelSwitch" "use" [
    local box boxState button
    box = (entquery "mapmodel" "triggerBox")
    boxState = (triggerstate $box)
    button = (entquery "mapmodel" "button_02")
    if (= $boxState 1) [
        triggerstate $box 0
        triggermapmodel $button 3
    ] [
        triggerstate $box 1
        triggermapmodel $button 1
    ]
    triggersound $eventsource 4
]

trigger "gramophone" "use" [
    local mapSound
    mapSound = (entquery "sound" "gramophone")
    triggerstate $mapSound 1
]

trigger "autoDoor" "proximity" [
    local door
    door = (entquery "mapmodel" "autoDoor")
    triggermapmodel $door 1
    triggersound $door 13
]
trigger "autoDoor" "distance" [
    local door
    door = (entquery "mapmodel" "autoDoor")
    triggermapmodel $door 3
    triggerstate $eventsource 1
    triggersound $door 14
]

trigger "toilet" "use" [
    triggersound $eventsource 15
]
